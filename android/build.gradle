import groovy.json.JsonSlurper

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}

apply plugin: 'com.android.library'

boolean exoplayer = false, cache = false, auto = false;
boolean dash = false, hls = false, smoothstreaming = false;

File file = file('../../../track-player.json')
if(file.exists()) {
    def json = new JsonSlurper().parseText(file.text)
    dash = json.dash ?: dash
    hls = json.hls ?: hls
    smoothstreaming = json.smoothstreaming ?: smoothstreaming
    auto = json.auto ?: auto

    exoplayer = json.exoplayer || dash || hls || smoothstreaming
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 16 // RN's minimum version
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }

        consumerProguardFiles 'proguard-rules.txt'
    }

    buildTypes {
        buildTypes.each {
            def autoMd = auto ? 'com.google.android.gms.car.application' : 'disabled.car.application'
            it.manifestPlaceholders = [autoMetadata: autoMd]
        }
    }
}

repositories {
    maven {
        // As RN is not updated in jcenter anymore, we'll have to grab it from npm
        url "../node_modules/react-native/android"
    }
    mavenCentral()
    jcenter()

    maven {
        url "https://dl.google.com/dl/android/maven2/"
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    compile('com.facebook.react:react-native:+') {
        exclude group: 'com.android.support', module: 'support-compat'
    }

    // ExoPlayer Core
    if(exoplayer) {
        compile 'com.google.android.exoplayer:exoplayer-core:2.7.1'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-core:2.7.1'
    }

    // ExoPlayer DASH
    if(dash) {
        compile 'com.google.android.exoplayer:exoplayer-dash:2.7.1'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-dash:2.7.1'
    }

    // ExoPlayer HLS
    if(hls) {
        compile 'com.google.android.exoplayer:exoplayer-hls:2.7.1'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-hls:2.7.1'
    }

    // ExoPlayer SmoothStreaming
    if(smoothstreaming) {
        compile 'com.google.android.exoplayer:exoplayer-smoothstreaming:2.7.1'
    } else {
        provided 'com.google.android.exoplayer:exoplayer-smoothstreaming:2.7.1'
    }

    // Make sure we're using at least the support library 27.1.0
    compile 'com.android.support:support-compat:27.1.0'
    compile 'com.android.support:support-media-compat:27.1.0'
}
